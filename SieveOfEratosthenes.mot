#Motmot

┌───────────────────────────────────────────────┐
│ Sieve of Eratosthenes & Twin Primes in Motmot │
│                                               │
│ Based upon ‘Infinite Data Structures’         │
│ with Graham Hutton                            │
│                                               │
│ Copyright 2020 K.D.P.Ross <KDPRoss@gmail.com> │
│                                               │
│ This code is licensed only for study and      │
│ personal enrichment.                          │
└───────────────────────────────────────────────┘


│ Relevant resources:
│ - Hutton talking about this on ‘Computerphile’: https://youtu.be/bnRNiE_OVWA
│ - ‘Why Functional Programming Matters’ (J.Hughes, 1990): tinyurl.com/k7r2m62


┌──────────────────────────────────────────┐
│ ‘The raison  d'être  of a program  is to │
│ be  a  mathematically-elegant  object of │
│ sublime beauty; it's also pretty cool if │
│ it  occasionally   computes  something.’ │
│                             - K.D.P.Ross │
└──────────────────────────────────────────┘



│ Hutton's code:
│ primes = sieve [2..]

primes : Stream Num =
  sieve { 2 .. }

│ Motmot doesn't have list comprehensions; let's write this
│ more idiomatically.
│ Hutton's code:
│ sieve (p : ps) = p : sieve [ x | x <- ps, mod x p /= 0 ]

sieve : Stream Num -> Stream Num =
  (p : Num) ::: (ps : Stream Num) ~
    const{ () } p ::: (() ~ sieve (filter-stream not-divisible-by-p ps))
    where (not-divisible-by-p : Num -> Bool) = (_ mod p) >>
                                               (0 =/= _)

│ Here, we have a somewhat different approach to the `twin`
│ function: The idiomatic Motmot is point-free. As well,
│ Motmot uses pipeline syntax, rather than all of the
│ brackets.
│ Hutton's code:
│ twins = filter twin (zip primes (tail primes))
│ twin (x, y) = y == x + 2

twin-primes : Stream (Num, Num) =
  primes |>
    drop-stream 1 >>
    zip-stream primes >>
    filter-stream twin
  where (twin : (Num, Num) -> Bool) = first{ Num } (2 + _) >>
                                      uncurry (=={ Num })

│ Fun fact: The 1000th prime is 7919

thousandth-prime : Num =
  primes |>
    progress-stream Nothing{ Num } >>
    drop-stream (1000 - 1) >>
    take-stream 1 >>
    stream->list >>
    head
